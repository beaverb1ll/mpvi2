
set(LIBFTDI_VERSION 1.4.8)
set(LIBFTDI_SHA256 "815d880c5ec40904f062373e52de07b2acaa428e54fece98b31e6573f5d261a0")

set(OUTPUT_DIR "${CMAKE_BINARY_DIR}/libftdi")

set(LIBFTDI_FILENAME "libftd2xx-${CMAKE_SYSTEM_PROCESSOR}-${LIBFTDI_VERSION}.gz")
set(LIBFTDI_URL "https://www.ftdichip.com/Drivers/D2XX/${CMAKE_SYSTEM_NAME}/${LIBFTDI_FILENAME}")

set(LIBFTDI_DOWNLOAD ON )
set(LIBFTDI_LOCAL_FILE "${OUTPUT_DIR}/${LIBFTDI_FILENAME}")

if( EXISTS "${LIBFTDI_LOCAL_FILE}" )
  file( SHA256 "${LIBFTDI_LOCAL_FILE}" LIBFTDI_LOCAL_SHA256 )

  if( "${LIBFTDI_LOCAL_SHA256}" STREQUAL "${LIBFTDI_SHA256}" )
    set( LIBFTDI_DOWNLOAD OFF )
  else()
    file( REMOVE "${LIBFTDI_LOCAL_FILE}" )
  endif()
endif()
if( LIBFTDI_DOWNLOAD )
  message( STATUS
    "Downloading libftdi ${LIBFTDI_VERSION} from ${LIBFTDI_URL}" )

  file(
    DOWNLOAD "${LIBFTDI_URL}" "${LIBFTDI_LOCAL_FILE}"
    SHOW_PROGRESS EXPECTED_HASH SHA256=${LIBFTDI_SHA256}
  )
else()
  message( STATUS "Using libftdi archive: ${LIBFTDI_LOCAL_FILE}" )
endif()

set(LIBFTDI_SO_PATH ${OUTPUT_DIR}/release/build/libftd2xx.so.${LIBFTDI_VERSION})

execute_process( COMMAND ${CMAKE_COMMAND} -E tar -zxf ${LIBFTDI_LOCAL_FILE} WORKING_DIRECTORY ${OUTPUT_DIR} )
execute_process( COMMAND ${CMAKE_COMMAND} -E create_symlink ${LIBFTDI_SO_PATH} ${OUTPUT_DIR}/release/build/libftd2xx.so)

set(FTDI_LIBRARIES ${LIBFTDI_SO_PATH})
set(FTDI_INCLUDE_DIR ${OUTPUT_DIR}/release/)
